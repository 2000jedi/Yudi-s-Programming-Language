<STATEMENTS>

<INIT> = INT
<INIT> = FLOAT
<INIT> = CHAR
<INIT> = STRING
<OPTIONAL_INIT> = <EPS>
<OPTIONAL_INIT> = ASSIGN <INIT>

<OPTIONAL_ARRAY_DEF> = <EPS>
<OPTIONAL_ARRAY_DEF> = LARR INT RARR
<OPTIONAL_ARRAY> = <EPS>
<OPTIONAL_ARRAY> = LARR INT RARR
<OPTIONAL_ARRAY> = LARR NAME RARR

<OPTIONAL_GENERIC_DEF> = <EPS>
<OPTIONAL_GENERIC_DEF> = GT NAME LT

<TYPENAME> = CHART <OPTIONAL_ARRAY_DEF>
<TYPENAME> = UINT8 <OPTIONAL_ARRAY_DEF>
<TYPENAME> = INT32 <OPTIONAL_ARRAY_DEF>
<TYPENAME> = NAME <OPTIONAL_ARRAY_DEF>

<VARDEF> = VARDEF NAME COLON <TYPENAME> <OPTIONAL_INIT> EOL
<CONSTDEF> = CONSTDEF NAME ASSIGN <INIT> EOL

<EVAL_EXPR> = INT
<EVAL_EXPR> = CHAR
<EVAL_EXPR> = NAME <OPTIONAL_FUNCCALL>

<EMPTY_EXPR> = EOL
<RET_EXPR> = RET <EVAL_EXPR>
<OPTIONAL_ELSE_EXPR> = <EPS>
<OPTIONAL_ELSE_EXPR> = ELSE LBRA <EXPRS> RBRA
<IF_EXPR> = IF LPAR <EVAL_EXPR> RPAR LBRA <EXPRS> RBRA <OPTIONAL_ELSE_EXPR>
<EXPR> = <IF_EXPR>
<EXPR> = <RET_EXPR>
<EXPR> = <EVAL_EXPR>
<EXPR> = <EMPTY_EXPR>

<EXPRS> = <EPS>
<EXPRS> = <EXPR> <EXPRS>

<OPTIONAL_FUNCCALL_CNT> = <EPS>
<OPTIONAL_FUNCCALL_CNT> = COMMA <EVAL_EXPR> <OPTIONAL_FUNCCALL_CNT>
<OPTIONAL_FUNCCALL> = <EPS>
<OPTIONAL_FUNCCALL> = LPAR <EVAL_EXPR> <OPTIONAL_FUNCCALL_CNT> RPAR

<RETURN_DEF> = <EPS>
<RETURN_DEF> = COLON <TYPENAME>

<PARAM> = NAME COLON <TYPENAME>
<MORE_PARAMS> = <EPS>
<MORE_PARAMS> = COMMA <PARAM> <MORE_PARAMS>
<PARAMS_DEF> = <EPS>
<PARAMS_DEF> = <PARAM> <MORE_PARAMS>

<FUNCDEF> = FUNCDEF NAME <OPTIONAL_GENERIC_DEF> LPAR <PARAMS_DEF> RPAR <RETURN_DEF> LBRA <EXPRS> RBRA

<STATEMENT> = <FUNCDEF>
<STATEMENT> = <VARDEF>
<STATEMENT> = <CONSTDEF>

<STATEMENTS> = <EPS>
<STATEMENTS> = <STATEMENT> <STATEMENTS>
