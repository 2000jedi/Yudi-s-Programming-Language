<STATEMENTS>

<OPTIONAL_INIT> = <EPS>
<OPTIONAL_INIT> = ASSIGN <EVAL_EXPR>

<OPTIONAL_ARRAY_DEF> = <EPS>
<OPTIONAL_ARRAY_DEF> = LARR INT RARR
<OPTIONAL_ARRAY> = <EPS>
<OPTIONAL_ARRAY> = LARR <EVAL_EXPR> RARR

<OPTIONAL_GENERIC_DEF> = <EPS>
<OPTIONAL_GENERIC_DEF> = LT NAME GT

<TYPENAME> = CHART <OPTIONAL_ARRAY_DEF>
<TYPENAME> = UINT8 <OPTIONAL_ARRAY_DEF>
<TYPENAME> = INT32 <OPTIONAL_ARRAY_DEF>
<TYPENAME> = VOIDT <OPTIONAL_ARRAY_DEF>
<TYPENAME> = NAME <OPTIONAL_ARRAY_DEF>

<VARDEF> = VARDEF NAME COLON <TYPENAME> <OPTIONAL_INIT> EOL
<CONSTDEF> = CONSTDEF NAME ASSIGN <EVAL_EXPR> EOL

<EMPTY_EXPR> = EOL
<RET_EXPR> = RET <EVAL_EXPR> EOL
<OPTIONAL_ELSE_EXPR> = <EPS>
<OPTIONAL_ELSE_EXPR> = ELSE LBRA <EXPRS> RBRA
<IF_EXPR> = IF LPAR <EVAL_EXPR> RPAR LBRA <EXPRS> RBRA <OPTIONAL_ELSE_EXPR>
<WHILE_EXPR> = WHILE LPAR <EVAL_EXPR> RPAR LBRA <EXPRS> RBRA
<FOR_EXPR> = FOR LPAR <EVAL_EXPR> EOL <EVAL_EXPR> EOL <EVAL_EXPR> RPAR LBRA <EXPRS> RBRA

<OPTIONAL_PARAMS_DEF> = <EPS>
<OPTIONAL_PARAMS_DEF> = LPAR <PARAMS_DEF> RPAR
<MORE_OPTIONS> = <EPS>
<MORE_OPTIONS> = COMMA NAME <OPTIONAL_PARAMS_DEF> <MORE_OPTIONS>
<OPTIONS> = <EPS>
<OPTIONS> = NAME <OPTIONAL_PARAMS_DEF> <MORE_OPTIONS>

<MATCH_OPTION> = <EPS>
<MATCH_OPTION> = LPAR <MATCH_OPTION_PARAM> RPAR
<MATCH_OPTION_PARAM> = NAME <MORE_MATCH_OPTION_PARAM>
<MORE_MATCH_OPTION_PARAM> = <EPS>
<MORE_MATCH_OPTION_PARAM> = COMMA NAME <MORE_MATCH_OPTION_PARAM>

<MATCH_LINES> = <EPS>
<MATCH_LINES> = NAME <MATCH_OPTION> COLON LBRA <EXPRS> RBRA <MATCH_LINES>
<MATCH_EXPR> = MATCH LPAR <EVAL_EXPR> RPAR LBRA <MATCH_LINES> RBRA
<ENUMDEF> = ENUMDEF NAME LBRA <OPTIONS> RBRA

<PARS> = INT
<PARS> = FLOAT
<PARS> = CHAR
<PARS> = STRING
<PARS> = NAME <OPTIONAL_FUNCCALL> <OPTIONAL_ARRAY>

<PARS> = LPAR <EVAL_EXPR> RPAR
<CDOT_> = <EPS>
<CDOT_> = DOT <PARS> <CDOT_>
<CDOT> = <PARS> <CDOT_>
<MUL_DIV_> = <EPS>
<MUL_DIV_> = MUL <CDOT> <MUL_DIV_>
<MUL_DIV_> = DIV <CDOT> <MUL_DIV_>
<MUL_DIV_> = REM <CDOT> <MUL_DIV_>
<MUL_DIV> = <CDOT> <MUL_DIV_>
<ADD_SUB_> = <EPS>
<ADD_SUB_> = ADD <MUL_DIV> <ADD_SUB_>
<ADD_SUB_> = SUB <MUL_DIV> <ADD_SUB_>
<ADD_SUB> = <MUL_DIV> <ADD_SUB_>
<LGTE_> = <EPS>
<LGTE_> = LE <ADD_SUB> <LGTE_>
<LGTE_> = LT <ADD_SUB> <LGTE_>
<LGTE_> = GE <ADD_SUB> <LGTE_>
<LGTE_> = GT <ADD_SUB> <LGTE_>
<LGTE> = <ADD_SUB> <LGTE_>
<EQ_NEQ_> = <EPS>
<EQ_NEQ_> = EQU <LGTE> <EQ_NEQ_>
<EQ_NEQ_> = NEQ <LGTE> <EQ_NEQ_>
<EQ_NEQ> = <LGTE> <EQ_NEQ_>
<BAND> = <EPS>
<BAND> = BAND <EQ_NEQ> <BAND>
<BITWISE_AND> = <EQ_NEQ> <BAND>
<BXOR> = <EPS>
<BXOR> = BXOR <BITWISE_AND> <BXOR>
<BITWISE_XOR> = <BITWISE_AND> <BXOR>
<BOR> = <EPS>
<BOR> = BOR <BITWISE_XOR> <BOR>
<BITWISE_OR> = <BITWISE_XOR> <BOR>
<LAND> = <EPS>
<LAND> = LAND <BITWISE_OR> <LAND>
<LOGICAL_AND> = <BITWISE_OR> <LAND>
<LOR> = <EPS>
<LOR> = LOR <LOGICAL_AND> <LOR>
<LOGICAL_OR> = <LOGICAL_AND> <LOR>
<ASSIGN> = <EPS>
<ASSIGN> = ASSIGN <LOGICAL_OR> <ASSIGN>
<EVAL_EXPR> = <LOGICAL_OR> <ASSIGN>

<EXPR> = <VARDEF>
<EXPR> = <CONSTDEF>
<EXPR> = <IF_EXPR>
<EXPR> = <WHILE_EXPR>
<EXPR> = <FOR_EXPR>
<EXPR> = <MATCH_EXPR>
<EXPR> = <RET_EXPR>
<EXPR> = <EMPTY_EXPR>
<EXPR> = <EVAL_EXPR> EOL

<EXPRS> = <EPS>
<EXPRS> = <EXPR> <EXPRS>

<OPTIONAL_FUNCCALL_CNT> = <EPS>
<OPTIONAL_FUNCCALL_CNT> = COMMA <EVAL_EXPR> <OPTIONAL_FUNCCALL_CNT>
<OPTIONAL_FUNCCALL> = <EPS>
<OPTIONAL_FUNCCALL> = LPAR <EVAL_EXPR> <OPTIONAL_FUNCCALL_CNT> RPAR

<RETURN_DEF> = <EPS>
<RETURN_DEF> = COLON <TYPENAME>

<PARAM> = NAME COLON <TYPENAME>
<MORE_PARAMS> = <EPS>
<MORE_PARAMS> = COMMA <PARAM> <MORE_PARAMS>
<PARAMS_DEF> = <EPS>
<PARAMS_DEF> = <PARAM> <MORE_PARAMS>
<FUNCDEF> = FUNCDEF NAME <OPTIONAL_GENERIC_DEF> LPAR <PARAMS_DEF> RPAR <RETURN_DEF> LBRA <EXPRS> RBRA

<CLASS_DOMAIN> = <EPS>
<CLASS_DOMAIN> = <CONSTDEF> <CLASS_DOMAIN>
<CLASS_DOMAIN> = <VARDEF> <CLASS_DOMAIN>
<CLASS_DOMAIN> = <FUNCDEF> <CLASS_DOMAIN>
<CLASS_DOMAIN> = <ENUMDEF> <CLASS_DOMAIN>
<CLASSDEF> = CLASSDEF NAME <OPTIONAL_GENERIC_DEF> LBRA <CLASS_DOMAIN> RBRA

<STATEMENT> = <FUNCDEF>
<STATEMENT> = <VARDEF>
<STATEMENT> = <CONSTDEF>
<STATEMENT> = <CLASSDEF>
<STATEMENT> = <ENUMDEF>

<STATEMENTS> = <EPS>
<STATEMENTS> = <STATEMENT> <STATEMENTS>
